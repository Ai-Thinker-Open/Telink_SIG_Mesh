
/* to tell the linker the program begin from __start label in cstartup.s, thus do not treat it as a unused symbol */
ENTRY(__start)

SECTIONS
{
    . = 0x0;
        .vectors :
        {
        *(.vectors)
        *(.vectors.*)	/* MUST as follows, when compile with -ffunction-sections -fdata-sections, session name may changed */
        }
        
    . = (((. + 15) / 16)*16);       /* must 16 byte align if use "AT" for ".text" */
        PROVIDE(_vector_end_ = . );
            
    . = (. * (1 - __BOOT_LOADER_EN)) + (__FW_RAMCODE_SIZE_MAX * __BOOT_LOADER_EN);
        PROVIDE(_ram_code_start_ = . );
        .ram_code :
        AT(_vector_end_)
        {
        *main.o(.ram_code)
        *main.o(.ram_code.*)
        *ota_fw_ow.o(.ram_code)
        *ota_fw_ow.o(.ram_code.*)
        *flash.o(.ram_code)
        *flash.o(.ram_code.*)
        *(.ram_code)
        *(.ram_code.*)
        }
        
    . = (((. + 15) / 16)*16);       /* must 16 byte align if use "AT" for ".text" */
        PROVIDE(_rstored_ = . );

        
    . = 0x4000;
    PROVIDE(_ramcode_size_ = . );
	PROVIDE(_ramcode_size_div_16_ = (. + 15 ) / 16);
	PROVIDE(_ramcode_size_div_256_compile_ = (. + 255) / 256);
	PROVIDE(_ramcode_size_div_16_align_256_ = ( (. + 255) / 256) * 16);
	PROVIDE(_ramcode_size_align_256_ = ( _ramcode_size_div_16_align_256_)* 16);

        . = (_rstored_ + __FW_OFFSET);
        .text :
          AT((_rstored_ * (1 - __BOOT_LOADER_EN)) + ((_vector_end_ + (_rstored_ - _ram_code_start_)) * __BOOT_LOADER_EN))  
        {
        *(.text)
        *(.text.*)
        }
        .rodata :
        {
        *(.rodata)
        *(.rodata.*)
        }

    . = (((. + 3) / 4)*4);
        PROVIDE(_dstored_ = .);                     /*location in flash*/
        PROVIDE(_code_size_div_256_ = (. + 255) / 256);
        PROVIDE(_dstored_bin_ = (((. - __FW_OFFSET) * (1 - __BOOT_LOADER_EN)) + ((_vector_end_ + (_dstored_ - _ram_code_start_)) * __BOOT_LOADER_EN)));   /*location in bin*/
        PROVIDE(_code_size_ = _dstored_bin_);
        PROVIDE(_ramcode_size_div_256_ = (_ramcode_size_div_256_compile_ * (1 - __MCU_RUN_SRAM_EN)) + (_code_size_div_256_ * __MCU_RUN_SRAM_EN));
	. = __RAM_START_ADDR + 0x900 + (_ramcode_size_div_256_ * 0x100);	/* 0x100 alighned, must greater than or equal to:0x808000 + ram_code_size +  irq_vector(0x100) + IC_tag(0x100) + IC_cache(0x800) == 0x808a00 + ram_code_size */
        .data :	 /*is retention for 825x. is 'data' for others. must use name 'data'.*/
          AT ( _dstored_bin_ )
        {
        . = (((. + 3) / 4)*4);
         PROVIDE(_start_data_ = . );
        *(.retention_data)
        *(.retention_data.*)
         *(.data);
         *(.data.*);
        . = (((. + 3) / 4)*4);
         PROVIDE(_end_data_ = . );
        }        

        .bss(NOLOAD) :	/*is retention for 825x. is 'bss' for others. must use name 'bss'.*/
        {
	    . = (((. + 3) / 4)*4);  /* new section, default 16 byte aligned*/
	    PROVIDE(_start_bss_ = .);
        *(.retention_bss)
        *(.retention_bss.*)
        *(.sbss)
        *(.sbss.*)
        *(.bss)
        *(.bss.*)
        }
        . = (((. + 3) / 4)*4);
    PROVIDE(_end_bss_ = .); /*_end_bss_ must outside of {}, if not it will not correct*/
          
        .retention_data : /*no retention data. only for 825x, use 'retention_data', because tdebug can't know private name*/
          AT (_dstored_bin_ + (_end_data_ -  _start_data_))
        {
        . = (((. + 3) / 4)*4);
        PROVIDE(_no_retention_data_start_ = . );
        *(.no_ret_data)
        *(.no_ret_data.*)
        . = (((. + 3) / 4)*4);
        PROVIDE(_no_retention_data_end_ = . );
        }
        
        .retention_bss(NOLOAD) : /*no retention data. only for 825x, use 'retention_bss', because tdebug can't know private name*/
          AT (_dstored_bin_ + (_end_data_ -  _start_data_) + (_no_retention_data_end_ - _no_retention_data_start_))
        {
        . = (((. + 3) / 4)*4);
        PROVIDE(_no_retention_bss_start_ = . );
        *(.no_ret_bss)
        *(.no_ret_bss.*)
        . = (((. + 3) / 4)*4);
        PROVIDE(_no_retention_bss_end_ = . );
        }

    PROVIDE(_bin_size_ = _code_size_ + (_end_data_ - _start_data_)+(_no_retention_data_end_ - _no_retention_data_start_));
    PROVIDE(_bin_size_div_16_ = (_bin_size_ + 15 ) / 16);
    PROVIDE(_ictag_start_ = __RAM_START_ADDR + (_ramcode_size_div_256_) * 0x100);		/*not for retention*/
    PROVIDE(_ictag_end_ = __RAM_START_ADDR + (_ramcode_size_div_256_ + 1) * 0x100);	/*not for retention*/
    PROVIDE(_bootloader_ram_code_end_ = (_ram_code_start_ + _code_size_ - _vector_end_));
    
	/*PROVIDE(_ram_use_size_div_16_ = (_retention_data_end_ - 0x840000  + 15 ) / 16); only for 825x*/
    ASSERT((_end_bss_ - 0x840000) * __PM_DEEPSLEEP_RETENTION_ENABLE <= 0x8000, "**Retention Area Overflow, Check your data/bss usages**")
    ASSERT((_bin_size_ + 16) * __FLASH_512K_ENABLE <= 0x30000, "**Error:firmware size Overflow!!!**")
    ASSERT((_ramcode_size_div_16_align_256_ * 16 * __FW_START_BY_BOOTLOADER_EN) <= __FW_RAMCODE_SIZE_MAX, "**Error:ramcode size Overflow!!!**")
}
